Module = [{ Decl# | Using | ";" }]
Using = "using" <Str> ";"

Code = ( "{" [{ Line }] "}" ) | Line
Line = If | While | DoWhile | For | Code | Decl# | ( [ ( "return" Value ) | "break" | "continue" | Value ] ";" )
If = "if" "(" Value ")" Code [ "else" Code ]
While = "while" "(" Value ")" Code
DoWhile = "do" Code "while" "(" Value ")" ";"
For := "for" "(" Decl# | ( [ Value ] ";" ) [ Value ] ";" [ Value ] ")" Code

Value = Comma
AssignValue = Assign
Comma = Assign [{ "," Assign }]
Assign = Ternary [ "&=" | "|=" | "^=" | ">>=" | "<<=" | "=" | "+=" | "-=" | "*=" | "/=" | "%=" Assign ]
Ternary = Bool [ "?" Value ":" Ternary ]
Bool = Equality [{ "&&" | "||" | "&" | "|" | "^" Equality }]
Equality = Shift [{ "==" | "!=" | ">" | ">=" | "<" | "<=" Shift }]
Shift = Expr [{ ">>" | "<<" Expr }]
Expr = Term [{ "+" | "-" Term }]
Term = Unary [{ "*" | "/" | "%" Unary }]
Unary = ( "!" | "~" | "-" | "*" | "&" | "++" | "--" Unary ) | PostUnary
PostUnary = Object [{ "++" | "--" }]
Object = Factor [{   ( "[" Value "]" )
                   | ( "(" [ AssignValue [{ "," AssignValue }] ] ")" )
                   | ( "." | "->" <Ident> ) }]
Factor =   ( "(" Value ")" )
         | ( "(" Type ")" Unary )
         | ( [ "(" Type ")" ] "{" [ ElementInit [{ "," ElementInit }] ] "}" )
         | ( "sizeof" ( "(" Type | Value ")" ) | Unary )
         | VAStart | VAEnd | VAArg | VACopy | ( "assert" "(" AssignValue ")" )
         | Lambda | <Int> | <Bool> | <Str> | <Char> | <Ident>
ElementInit = [   (   ( "." <Ident> )
                    | ( "[" Value "]" ) DesignatedInit )
                | AssignValue ]
DesignatedInit = "=" AssignValue
Lambda = "[" "]" ParamList
         ( "{" Code "}" ) | ( "(" Value ")" )
VAStart = va_start "(" AssignValue "," <Ident> ")"
VAEnd = va_end "(" AssignValue ")"
VAArg = va_arg "(" AssignValue "," Type ")"
VACopy = va_copy "(" AssignValue "," AssignValue ")"

Type = DeclBasic DeclExpr#
Decl = Storage DeclBasic ";" | ( DeclExpr#   ( [{ "," DeclExpr# }] ";" )
                                           | FnImpl )
Storage = [ "auto" | "static" | "extern" | "typedef" ]
FnImpl = Code

Field = DeclBasic [ DeclExpr# [{ "," DeclExpr# }] ] ";"
EnumField = Name# [ "=" AssignValue ]
Param = DeclBasic DeclExpr#

DeclBasic = [ "const" ] <Ident> | StructOrUnion | Enum
StructOrUnion = "struct" | "union" Name# ^ ( "{" [{ Field }] "}" )
Enum = "enum" Name# ^ ( "{" EnumField [{ "," EnumField }] [ "," ] "}" )

DeclExpr = DeclUnary [ "=" AssignValue ]
DeclUnary = ( "*" | "const" DeclUnary ) | DeclObject
DeclObject = DeclAtom [{ ParamList | ( "[" Value "]" ) }]
ParamList = "(" [ ( Param [{ "," Param }] [ "," "..." ] ) | "..." | "void" ] ")"
DeclAtom = [ ( "(" DeclExpr ")" ) | Name ]
Name = <UnqualifiedIdent>
